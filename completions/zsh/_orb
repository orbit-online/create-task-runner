#compdef orb

if [ -z "$_ORB_LOCAL_INVOKE" ]; then
    _orb() {
        local -a tasks=()

        __orb_complete_tasks() {
            local task_path
            while IFS= read -r; do
                task_path=$(basename "$REPLY")
                tasks+=("${task_path%.*}:${REPLY#$PWD/}")
            done < <(orb --list)
        }

        __orb_complete_next_task() {
            local task_basename task_completion \
                  task_dir task_name=$1 task_path

            task_path=$(orb --list "$task_name" | tr '\0' '\n')
            [[ -z $task_path ]] && return 0
            (( $(wc -l <<< "$task_path" ) > 1 )) && return 0

            task_path="$(realpath "$task_path")"
            task_dir="$(dirname "$task_path")"
            task_basename="$(basename "${task_path}")"
            task_completion="${task_basename%.*}.orb_zsh_completion"

            if [ -f "${task_dir}/${task_completion}" ]; then
                eval "$(orb --env)"
                # shellcheck disable=SC1090
                _ORB_ZSH_COMPLETION=1 source "${task_dir}/${task_completion}" && \
                _exec_orb_zsh_completion; \
                unset _exec_orb_zsh_completion
            fi

            return 0
        }

        if (( CURRENT == 2)); then
            __orb_complete_tasks
            _describe 'tasks' tasks
        elif (( CURRENT > 2 )); then
            # shellcheck disable=SC2154
            __orb_complete_next_task "${words[2]}"
        fi

        return 0
    }

    _orb
fi
